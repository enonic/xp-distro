import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'base'
    id 'maven-publish'
    id 'com.enonic.defaults' version '2.1.6'
    id 'de.undercouch.download' version '5.6.0'
    id 'distribution'
}

group = 'com.enonic.xp'

repositories {
    mavenLocal()
    maven {
        url = 'https://repo.enonic.com/dev'
    }
}

apply from: 'jdk.gradle'

configurations {
    app
    distro
}

dependencies {
    app "com.enonic.xp:app-main:${version}@jar"
    app "com.enonic.xp:app-standardidprovider:${version}@jar"
    app "com.enonic.xp:app-applications:${version}@jar"
    app "com.enonic.xp:app-users:${version}@jar"
    if ( isSdkBuild() )
    {
        app "com.enonic.xp:app-sdk:${version}@jar"
    }
    distro( "com.enonic.xp:runtime:${version}@zip" )
}

ext {
    targetBaseName = getTargetBaseName()
    appLevel = 40
    downloadJdkDir = layout.buildDirectory.dir('jdk').get()
    jdkPackageFile = getJdkPackageFile()
    jdkPackageName = getJdkPackageName()
    unpackedJdkDir = downloadJdkDir.dir(jdkPackageName)
    jlinkDir = layout.buildDirectory.dir("jlink/$jdkPackageName/${getTargetTypeName()}").get()
}

tasks.register('downloadJdk', Download) {
    description = 'Download JDK.'
    group = 'Dist'

    onlyIf {
        withJava()
    }

    overwrite false
    onlyIfModified true

    if ( withJava() )
    {
        src getJdkDownloadUrl()
        dest downloadJdkDir.file(jdkPackageFile)
        doFirst {
            mkdir downloadJdkDir
        }
    }
}

tasks.register('unpackJdk', Copy) {
    dependsOn tasks.named('downloadJdk')
    description = 'Unpack JDK.'
    group = 'Dist'

    onlyIf {
        withJava()
    }

    if ( withJava() )
    {
        def fileTree
        if ( jdkPackageFile.endsWith( '.zip' ) )
        {
            fileTree = zipTree( downloadJdkDir.file(jdkPackageFile) )
        }
        else if ( jdkPackageFile.endsWith( '.tar.gz' ) )
        {
            fileTree = tarTree( resources.gzip( downloadJdkDir.file(jdkPackageFile) ) )
        }
        else
        {
            throw new GradleException( "Unable to unpack JDK: $jdkPackageFile" )
        }

        from( fileTree ) {
            eachFile { fcd -> fcd.relativePath = new RelativePath( true, fcd.relativePath.segments.drop( 1 ) )
            }
            includeEmptyDirs = false
            duplicatesStrategy = DuplicatesStrategy.WARN
        }

        into unpackedJdkDir
    }
}

tasks.register('jlink', Exec) {
    dependsOn tasks.named('unpackJdk')
    onlyIf {
        withJava()
    }
    doFirst {
        def archiveJavaHomeDir = getArchiveJavaHome() ? unpackedJdkDir.dir(getArchiveJavaHome()) : unpackedJdkDir
        def jmodsDir = "$archiveJavaHomeDir/jmods"

        delete jlinkDir

        def args = ['jlink', '--compress=zip-6', '--no-header-files', '--no-man-pages',
                    '--add-modules', 'ALL-MODULE-PATH',
                    '--module-path', "$jmodsDir",
                    '--output', "$jlinkDir"]

        args.add('--add-options=-XX:+UnlockExperimentalVMOptions -XX:+EnableJVMCIProduct -XX:+EnableJVMCI -XX:-UnlockExperimentalVMOptions -XX:ThreadPriorityPolicy=1')

        commandLine args
    }

    if (withJava()) {
        inputs.dir unpackedJdkDir
        outputs.dir jlinkDir
    }
}

distTar {
    compression = Compression.GZIP
    archiveExtension = "tgz"
}

distributions {
    main {
        distributionBaseName = project.ext.targetBaseName
        contents {

            into( '' ) {
                from (configurations.distro.collect { zipTree( it ) }) {
                    filesMatching( '**/*.sh' ) {
                        permissions { unix '0755' }
                    }
                }
                if ( isArm64() )
                {
                    exclude { (it.file.name == 'jna-4.1.0.jar') }
                }
            }
            into( "system/$appLevel" ) {
                from configurations.app
            }
            into( '' ) {
                from( 'src/common' ) {
                    filesMatching( '**/*.sh' ) {
                        filter( ReplaceTokens, tokens: [version: version] )
                        permissions { unix '0755' }
                    }
                    filesMatching( 'service/init.d/xp' ) {
                        permissions { unix '0755' }
                    }
                    filesMatching( 'README.txt' ) {
                        expand version: version
                    }
                }

                if ( !withJava() )
                {
                    exclude 'bin/setenv.bat'
                    exclude 'bin/setenv.sh'
                }
            }

            if ( withJava() )
            {
                into( "jdk" ) {
                    from( jlinkDir ) {
                        filesMatching( 'bin/*' ) {
                            permissions { unix '0755' }
                        }
                    }
                }
            }
            else
            {
                into( '' ) {
                    from( 'src/generic' ) {
                        filesMatching( '**/*.sh' ) {
                            permissions { unix '0755' }
                        }
                    }
                }
            }
        }
    }
}

[distZip, distTar, installDist]*.dependsOn jlink

publishing {
    publications {
        mavenJava( MavenPublication ) {
            artifactId = project.ext.targetBaseName
            if ( withDistTar() )
            {
                artifact distTar
            }
            if ( withDistZip() )
            {
                artifact distZip
            }
        }
    }
}
